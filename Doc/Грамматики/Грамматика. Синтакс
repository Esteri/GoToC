//------------------- ВЫРАЖЕНИЯ

<операнд 1ур> --> 
		  <операнд 2ур> <знак операции или> <операнд 1ур>
		| <операнд 2ур>

<операнд 2ур> --> 
		  <операнд 3ур> <знак операции и> <операнд 2ур>
		| <операнд 3ур>

<операнд 3ур> --> 
		  <операнд 4ур> <знак операции сравнения> <операнд 4ур>
		| <литерал> 
		| <открывающая круглая скобка> <операнд 1ур> <закрывающая круглая скобка>
		| <знак операции отрицания> <открывающая круглая скобка> <операнд 1ур> <закрывающая круглая скобка>

<операнд 4ур> --> 
		  <операнд 5ур> <знак операции сложения> <операнд 4ур>
		| <операнд 5ур>

<операнд 5ур> --> 
		  <операнд 6ур> <знак операции умножения> <операнд 5ур>
		| <операнд 6ур>

<операнд 6ур> --> 
		  <идентификатор>
		| <идентификатор> <открывающая круглая скобка> <закрывающая круглая скобка>
		| <идентификатор> <открывающая круглая скобка> <аргументы> <закрывающая круглая скобка>
		| <литерал>
		| <открывающая круглая скобка> <операнд 4ур> <закрывающая круглая скобка>

<аргументы> --> 
		  <операнд 1ур> 
		| <операнд 1ур> <запятая> <аргументы>


//------------------- ВЫСКАЗЫВАНИЯ

<высказывание> --> 
	  <объявление переменной>
	| <высказывание if>
	| <высказывание switch>
	| <высказывание for>
	| <идентификатор return> <конец высказывания> 
	| <идентификатор return> <операнд 1ур> <конец высказывания>
	| <идентификатор> <знак операции присваивания> <операнд 1ур> <конец высказывания>
	| <идентификатор> <открывающая круглая скобка> <закрывающая круглая скобка> <конец высказывания>
	| <идентификатор> <открывающая круглая скобка> <аргументы> <закрывающая круглая скобка> <конец высказывания>


<высказывания> --> <высказывание> | <высказывание> <высказывания>

//------------------- ОБЪЯВЛЕНИЯ

<объявление переменной> --> 
      <идентификатор const> <идентификатор> <идентификатор типа> <знак операции присваивания> <литерал> <конец высказывания>
	| <идентификатор var> <идентификатор> <идентификатор типа> <знак операции присваивания> <операнд 1ур> <конец высказывания>

<объявление функции> --> 
      <идентификатор func> <идентификатор> <открывающая круглая скобка> <параметры> <закрывающая круглая скобка> 
                                                               <идентификатор типа> <открывающая фигурная скобка> <конец высказывания>
      <высказывания> 
      <закрывающая фигурная скобка> <конец высказывания>

    | <идентификатор func> <идентификатор> <открывающая круглая скобка> <параметры> <закрывающая круглая скобка> 
                                                                                    <открывающая фигурная скобка> <конец высказывания>
      <высказывания> 
      <закрывающая фигурная скобка> <конец высказывания>

    | <идентификатор func> <идентификатор> <открывающая круглая скобка> <закрывающая круглая скобка> 
                                                   <идентификатор типа> <открывающая фигурная скобка> <конец высказывания>
      <высказывания> 
      <закрывающая фигурная скобка> <конец высказывания>

    | <идентификатор func> <идентификатор> <открывающая круглая скобка> <закрывающая круглая скобка> 
                                                                        <открывающая фигурная скобка> <конец высказывания>
      <высказывания> 
      <закрывающая фигурная скобка> <конец высказывания>

<параметры> --> 
      <идентификатор> <идентификатор типа> 
    | <идентификатор> <идентификатор типа> <запятая> <параметры>

//------------------- IF

<высказывание if> --> 
	| <идентификатор if> <операнд 1ур> <открывающая фигурная скобка> <конец высказывания> 
      <высказывания>
      <закрывающая фигурная скобка> <конец высказывания>

	| <идентификатор if> <операнд 1ур> <открывающая фигурная скобка> <закрывающая фигурная скобка> <конец высказывания>

	| <идентификатор if> <операнд 1ур> <открывающая фигурная скобка> <конец высказывания> 
      <высказывания>
      <закрывающая фигурная скобка> <ветка else>

	| <идентификатор if> <операнд 1ур> <открывающая фигурная скобка> <конец высказывания> 
      <закрывающая фигурная скобка> <ветка else> 

<ветка else> --> 
      <идентификатор else> <высказывание if>

    | <идентификатор else> <открывающая фигурная скобка> <конец высказывания>
      <высказывания>
      <закрывающая фигурная скобка> <конец высказывания>

    | <идентификатор else> <открывающая фигурная скобка> <закрывающая фигурная скобка> <конец высказывания>

//------------------- FOR

<высказывание for> --> 
	  <идентификатор for> <операнд 1ур> <открывающая фигурная скобка> <конец высказывания> 
      <высказывания>
      <закрывающая фигурная скобка> <конец высказывания>

	| <идентификатор for> 
      <идентификатор> <знак операции короткого присваивания> <операнд 1ур> <точка с запятой>
      <операнд 1ур> <точка с запятой>
      <идентификатор> <знак операции присваивания> <операнд 1ур> <открывающая фигурная скобка> <конец высказывания>
      <высказывания>
      <закрывающая фигурная скобка> <конец высказывания>

    | <идентификатор for> <операнд 1ур> <открывающая фигурная скобка> <закрывающая фигурная скобка> <конец высказывания>

	| <идентификатор for> 
      <идентификатор> <знак операции короткого присваивания> <операнд 1ур> <точка с запятой>
      <операнд 1ур> <точка с запятой>
      <идентификатор> <знак операции присваивания> <операнд 1ур> <открывающая фигурная скобка> <закрывающая фигурная скобка> <конец высказывания>

//------------------- SWITCH

<высказывание switch> -->
	  <идентификатор switch> <операнд 1ур> <открывающая фигурная скобка> <конец высказывания>
      <ветвление case>
      <закрывающая фигурная скобка> <конец высказывания>

<ветвление case> --> 
      <ветки case> 
    | <ветки case> <идентификатор default> <двоеточие> <конец высказывания> <высказывания>

<ветки case> --> 
      <идентификатор case> <операнд 1ур> <двоеточие> <конец высказывания>
      <высказывания> 
    
    | <идентификатор case> <операнд 1ур> <двоеточие> <конец высказывания>
      <высказывания> 
      <ветки case>

//------------------- АКСИОМА

<аксиома> --> <идентификатор import> <идентификатор fmt> <конец высказывания> <объявления> | lambda

<объявления> --> 
      <объявление переменной> <объявления> 
    | <объявление функции> <объявления> 
    | <объявление функции>
